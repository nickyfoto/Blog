---
layout: post
title:  "61AS笔记"
date:   2016-02-10
author: "Huang Qiang"
tags: [SICP, 61AS, lisp]
---

What is This Course About?

A course about managing complexity

* Mastering abstraction
* Programming paradigms

* 先全部展开最后计算叫做*normal-order evaluation*
* 先evaluate arguments然后apply method，叫做*applicative-order evalutation*。

  * Evaluate the operator and operands
  * Apply the operator to the operands

```scheme
(define (p) (p))

(define (test x y)
  (if (= x 0)
      0
      y))
      
(test 0 (p))
```
这里如果是先evaluate arguments的话就陷入了infinite loop。

if 和 cond 的区别

A minor difference between if and cond is that the <e> part of each cond clause may be a sequence of expressions. If the corresponding <p> is found to be true, the expressions <e> are evaluated in sequence and the value of the final expression in the sequence is returned as the value of the cond. In an if expression, however, the <consequent> and <alternative> must be single expressions.

* `and` returns the last element if everything was not false
* `or` returns the first true element and returns #f otherwise

---
## 2.2  Hierarchical Data and the Closure Property

The ability to create **pairs** whose elements are **pairs** is the essence of list structure's importance as a representational tool. We refer to this ability as the *closure property* of cons. 

In general, an operation for combining data objects satisfies the closure property if the results of combining things with that operation can themselves be combined using the same operation.

建立序对本身的元素也是序对。更一般的来说，通过一个操作组合起来的到的数据对象本身，还可以用这样的操作再进行组合并的到同样性质的数据对象。这种操作满足闭包性质。

2.2.1 给出了一些对表的操作。包括

* list-ref
* length
* append

以及对表的映射

* scale-list
* map

map的定义

```scheme
(define (map proc items)
  (if (null? items)
      nil
      (cons (proc (car items))
            (map proc (cdr items)))))

;scheme自身的map可以带多个items作为参数            
(map + (list 1 2 3) (list 40 50 60) (list 700 800 900))
(741 852 963)

(map (lambda (x y) (+ x (* 2 y)))
     (list 1 2 3)
     (list 4 5 6))
(9 12 15)
```


在scheme中，list就是tree。所以计算一个tree有多少个leaves可以写成

```scheme
(define (count-leaves x)
  (cond ((null? x) 0)  
        ((not (pair? x)) 1)
        (else (+ (count-leaves (car x))
                 (count-leaves (cdr x))))))
```
除了对list可以scale，对tree同样可以scale。

不用map的版本

```scheme
(define (scale-tree tree factor)
  (cond ((null? tree) nil)
        ((not (pair? tree)) (* tree factor))
        (else (cons (scale-tree (car tree) factor)
                    (scale-tree (cdr tree) factor)))))
```
用map的版本

```scheme
(define (scale-tree tree factor)
  (map (lambda (sub-tree)
         (if (pair? sub-tree)
             (scale-tree sub-tree factor)
             (* sub-tree factor)))
       tree))
```

### 2.2.3 Sequences as Conventional Interfaces


把序列作为一种约定的界面。

先看两个例子

在一棵树的叶子中，计算所有为奇数的叶子的平方的和。

```scheme
(define (sum-odd-squares tree)
  (cond ((null? tree) 0)  
        ((not (pair? tree))
         (if (odd? tree) (square tree) 0))
        (else (+ (sum-odd-squares (car tree))
                 (sum-odd-squares (cdr tree))))))
```
所有偶数的fib数

```scheme
(define (even-fibs n)
  (define (next k)
    (if (> k n)
        nil
        (let ((f (fib k)))
          (if (even? f)
              (cons f (next (+ k 1)))
              (next (+ k 1))))))
  (next 0))
```
它们都遵循一个pattern，先枚举，再filter或者map，最后把结果accumulate起来。

```scheme
;filter的定义
(define (filter predicate sequence)
  (cond ((null? sequence) nil)
        ((predicate (car sequence))
         (cons (car sequence)
               (filter predicate (cdr sequence))))
        (else (filter predicate (cdr sequence)))))

(filter odd? (list 1 2 3 4 5))
(1 3 5)        
        
;accumulate的定义
(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))
(accumulate + 0 (list 1 2 3 4 5))
15
(accumulate * 1 (list 1 2 3 4 5))
120
(accumulate cons nil (list 1 2 3 4 5))
(1 2 3 4 5)                  
```
枚举的定义

```scheme
;生成一个list
(define (enumerate-interval low high)
  (if (> low high)
      nil
      (cons low (enumerate-interval (+ low 1) high))))
(enumerate-interval 2 7)
(2 3 4 5 6 7)

;对树leaves的枚举
(define (enumerate-tree tree)  (cond ((null? tree) nil)        ((not (pair? tree)) (list tree))≦        (else (append (enumerate-tree (car tree))                      (enumerate-tree (cdr tree))))))

(enumerate-tree (list 1 (list 2 (list 3 4)) 5))
(1 2 3 4 5)                      
```

#### Nested Mappings

在1和n之间生成 1 ≦ j < i ≦ 的序对n，以1到3为例。

```scheme
(accumulate append
            nil
            (map (lambda (i)
                   (map (lambda (j) (list i j))
                        (enumerate-interval 1 (- i 1))))
                 (enumerate-interval 1 3)))
'((2 1) (3 1) (3 2))                 
```
这里用了两个嵌套的map，lambda的值又有组合，同时还加入了accumulate和enumerate-intverval，不免一下子让人难以理解。这里我们可以把append去掉，把map先摘出来。

```scheme
(map (lambda (i) 
       (map (lambda (j) (list i j))
            (enumerate-interval 1 (- i 1))))
     (enumerate-interval 1 3))
;因为(enumerate-interval 1 3)就是'(1 2 3)，所以这个procedure就是  
(map (lambda (i) 
       (map (lambda (j) (list i j))
            (enumerate-interval 1 (- i 1))))
     '(1 2 3))
;把第一个map拆开，把i的值带入，得到
(map (lambda (j) (list 1 j)) (enumerate-interval 1 (- 1 1)))
(map (lambda (j) (list 2 j)) (enumerate-interval 1 (- 2 1)))
(map (lambda (j) (list 3 j)) (enumerate-interval 1 (- 3 1)))

;这三个map的到的结果是        
'()
'((2 1))
'((3 1) (3 2))
;这里注意，(enumerate-interval 1 0)返回'()，(map proc '())并不会报错，而是返回'()。所以这个procedure最终返回
'(() ((2 1)) ((3 1) (3 2))) ;长度为4，第一项为'()
(accumulate append '() '(() ((2 1)) ((3 1) (3 2))) 
'((2 1) (3 1) (3 2)) ;返回
```
由于这类程序里经常先映射后append，所以把他们定义为一个procedure。

```scheme
(define (flatmap proc seq)
  (accumulate append nil (map proc seq)))
```
看的到的这些pair的sum是不是prime，作为filter的proc

```scheme
(define (prime-sum? pair)
  (prime? (+ (car pair) (cadr pair))))
```
最终返回结果。注意，这里用map制作成需要返回的各式，而没用accumulate

```scheme
(define (make-pair-sum pair)
  (list (car pair) (cadr pair) (+ (car pair) (cadr pair))))
```
最后组合在一起

```scheme
(define (prime-sum-pairs n)
  (map make-pair-sum
       (filter prime-sum?
               (flatmap
                (lambda (i)
                  (map (lambda (j) (list i j))
                       (enumerate-interval 1 (- i 1))))
                (enumerate-interval 1 n)))))
(prime-sum-pairs 6);返回 
'((2 1 3) (3 2 5) (4 1 5) (4 3 7) (5 2 7) (6 1 7) (6 5 11))
                
```
嵌套的映射还可以用来生成一个set的所有排列

```scheme
(define (permutations s)
  (if (null? s)                    ; empty set?
      (list nil)                   ; sequence containing empty set
      (flatmap (lambda (x)
                 (map (lambda (p) (cons x p))
                      (permutations (remove x s))))
               s)))

(permutations '(1 2 3))

;在一个序列中除去某个元素。
(define (remove item sequence)
  (filter (lambda (x) (not (= x item)))
          sequence))
          


'((1 2 3) (1 3 2) (2 1 3) (2 3 1) (3 1 2) (3 2 1))
```
同样，还有计算一个set的所有子集

```scheme
(define (subsets s)
  (if (null? s)
      (list nil)
      (let ((rest (subsets (cdr s))))
        (append rest (map (lambda (x)
                            (cons (car s) x)) rest)))))
(subsets '(1 2 3))
'(() (3) (2) (2 3) (1) (1 3) (1 2) (1 2 3))                            
```
为什么这个有效？

取一个set的所有子集(powerset)的办法：

1. 计算除第一个元素之外，这个set的所有子集
2. 把第一个元素prepend在上述子集的前面。
3. 把这两部分和在一起

例如，我有一个set，（1），那么它的所有子集是(nil (1))。
如果我的set是(2 1)，那么，除了2的所有子集我们已经知道了， 是(nil (1))。
把2加在这上面，就是((2) (2 1))。
再与(1)的所有子集合并(nil (1) (2) (2 1))

现在我们来解著名的八皇后谜题。

八皇后就是把八个皇后摆在国际象棋棋盘上，让任意两个皇后都不在同一行，同一列，或者同一对角线。

---









racket所包涵的 *pair* 被称为 compound structure。这在其它语言中并不常见。在2.1.3中作者把 data 定义为 defined by some collection of selectors and constructors, together with specified conditions that these procedures must fulfill in order to be a valid representation。同时作者也提到，this idea is very difficult to formulate rigorously。

###2.2.1 Representing Sequences

A sequence of pairs, formed by nested conses, is called a list。
它就是链表。它也有一些operation，list-ref，length，append，list? 等。这里注意 list 一定是 pair **（nil除外）**，但 pair 不一定是 list。所以注意 (list?和pair?）的区分使用。

The `cdr` of the final pair signals the end of the sequence by pointing to a distinguished value `nil`. 同时注意 `nil`和`null`和`'()`是完全相同的。

`null?`用来测试这个list是不是empty list。可以和`empty?`互换。

在连接两个list时，根据需要多用list或者append。

```scheme
-> (list (list 1 2) (list 3 4))
'((1 2) (3 4))
-> (append (list 1 2) (list 3 4))
'(1 2 3 4)
-> (cons (list 1 2) (list (list 3 4))) ;违反直觉
'((1 2) (3 4))
-> (cdr (cons (list 1 2) (list (list 3 4))))
'((3 4))
```

**因为用`cons`来连接两个list，会有违反直觉的事情发生。特别是有python list经验的人。**

```scheme
-> (equal? '((1 2) 3 4) (cons (list 1 2) (list 3 4)))
#t
-> (length (cons (list 1 2) (list 3 4)))
3
```
在list中，`cdr`永远取不到单个元素

```scheme
-> (cdr (cons (list 1 2) (list 3 4)))
'(3 4)
-> (cdr (cdr (cons (list 1 2) (list 3 4))))
'(4)
-> (cdr (cdr (cdr (cons (list 1 2) (list 3 4)))))
'()
```
---

**accumulate**

```scheme
(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))

(accumulate + 0 (list 1 2 3 4 5))
15
(accumulate * 1 (list 1 2 3 4 5))
120
(accumulate cons nil (list 1 2 3 4 5))
(1 2 3 4 5)
```
accumulate看似简单，但想用好并不容易但是SICP 2.2.3中的练习却全部靠它来完成。

我们拿第一个做例子

```scheme
(accumulate + 0 (list 1 2 3 4 5))
(+ 1 (accumulate + 0 '(2 3 4 5)))               ;(+ 1 14)
     (+ 2 (accumulate + 0 '(3 4 5)))            ;(+ 2 12)
          (+ 3 (accumulate + 0 '(4 5)))         ;(+ 3 9)
               (+ 4 (accumulate + 0 '(5)))      ;(+ 4 5)
                    (+ 5 (accumulate + 0 '()))) ;(+ 5 0)
                         0 ;当sequence是null时，返回initial，也就是0。
```
也就是说，`op`一定带有两个参数。`initial` 通常是0，1 或者`'()`。`foldr`是accumulate在racket中的同名procedure。意思是从左fold到右边，但其实第一次返回值的是`initial`和`'()`，然后是`(op initial 最右边的值)`。

如果`op`是`+`或者`*`从左到右从右到左结果一样，但若是`/`或是更复杂的`lambda`，顺序就尤其重要了。


实数，是有理数和无理数的总称，前者如0、-4、81/7；后者如√2、π等。

虚数，即实数部分为0的复数。

实数和虚数共同构成复数。